plugins {
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'jacoco'
    id 'me.champeau.gradle.jmh' version '0.5.3'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'com.github.spotbugs' version "6.0.14"
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    jacoco {
        toolVersion = '0.8.7'
    }
}

allprojects {
    group = 'com.optimizely.ab'

    def github_tagged_version = System.getenv('GITHUB_TAG')
    if (github_tagged_version != null) {
        version = github_tagged_version
    }

    ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
}

def publishedProjects = subprojects.findAll { it.name != 'java-quickstart' }

configure(publishedProjects) {
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'me.champeau.gradle.jmh'
    apply plugin: 'com.github.hierynomus.license'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    spotbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    spotbugs {
        spotbugsJmh.enabled = false
        reportLevel = com.github.spotbugs.snom.Confidence.valueOf('HIGH')
    }

    test {
        testLogging {
            showStandardStreams = false
        }
    }

    jmh {
        duplicateClassesStrategy = 'warn'
    }

    sourceSets {
        jmh.java.srcDirs += sourceSets.test.java.srcDirs
    }

    tasks.named('compileJmhJava') {
        dependsOn processTestResources, compileTestJava
    }

    dependencies {
        jmh 'org.openjdk.jmh:jmh-core:1.12'
        jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.12'
    }

    dependencies {
        implementation group: 'commons-codec', name: 'commons-codec', version: commonCodecVersion

        testImplementation group: 'junit', name: 'junit', version: junitVersion
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: hamcrestVersion
        testImplementation group: 'com.google.guava', name: 'guava', version: guavaVersion

        // logging dependencies (logback)
        testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        testImplementation group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion

        testImplementation group: 'com.google.code.gson', name: 'gson', version: gsonVersion
        testImplementation group: 'org.json', name: 'json', version: jsonVersion
        testImplementation group: 'com.googlecode.json-simple', name: 'json-simple', version: jsonSimpleVersion
        testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    }

    configurations.all {
        resolutionStrategy {
            force "junit:junit:${junitVersion}"
            force 'com.netflix.nebula:nebula-gradle-interop:2.2.2'
        }
    }

    def docTitle = "Optimizely Java SDK"
    if (name.equals('core-httpclient-impl')) {
        docTitle = "Optimizely Java SDK: Httpclient"
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    customizePom(pom, docTitle)

                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                }
            }
        }

        signing {
            // base64 for workaround travis escape chars issue
            def signingKeyBase64 = System.getenv('MAVEN_SIGNING_KEY_BASE64')
            // skip signing for "local" version into MavenLocal for test-app
            if (!signingKeyBase64?.trim()) return
            byte[] decoded = signingKeyBase64.decodeBase64()
            def signingKey = new String(decoded)

            def signingPassword = System.getenv('MAVEN_SIGNING_PASSPHRASE')
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications.release
        }
    }

    license {
        header = rootProject.file("resources/HEADER")
        skipExistingHeaders = true
        include "**/*.java"
        ext.author = "Optimizely"
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }

    task ship() {
        dependsOn('publish')
    }

    // concurrent publishing (maven-publish) causes an issue with maven-central repository
    // - a single module splits into multiple staging repos, so validation fails.
    // - adding this ordering requirement forces sequential publishing processes.
    project(':core-api').javadocJar.mustRunAfter = [':core-httpclient-impl:ship']
}

task ship() {
    dependsOn(':core-httpclient-impl:ship', ':core-api:ship', 'publishToSonatype', 'closeSonatypeStagingRepository')
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri('https://ossrh-staging-api.central.sonatype.com/service/local/'))
            snapshotRepositoryUrl.set(uri('https://central.sonatype.com/repository/maven-snapshots/'))
            username = System.getenv('MAVEN_CENTRAL_USERNAME')
            password = System.getenv('MAVEN_CENTRAL_PASSWORD')
        }
    }
}

// Note: Aggregate JaCoCo reporting has been removed due to type resolution issues with Gradle 8.x
// Individual subprojects still generate their own coverage reports via the jacoco plugin

// standard POM format required by MavenCentral
def customizePom(pom, title) {
    pom.withXml {
        asNode().children().last() + {
            // keep this - otherwise some properties are not made into pom properly
            resolveStrategy = Closure.DELEGATE_FIRST

            name title
            url 'https://github.com/optimizely/java-sdk'
            description 'The Java SDK for Optimizely Feature Experimentation, Optimizely Full Stack (legacy), and Optimizely Rollouts'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'optimizely'
                    name 'Optimizely'
                    email 'optimizely-fullstack@optimizely.com'
                }
            }
            scm {
                connection 'scm:git:git://github.com/optimizely/java-sdk.git'
                developerConnection 'scm:git:ssh:github.com/optimizely/java-sdk.git'
                url 'https://github.com/optimizely/java-sdk.git'
            }
        }
    }
}
